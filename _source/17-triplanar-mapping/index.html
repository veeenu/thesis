---
layout: proto
title: "Triplanar procedural texture mapping"
---
<script type='application/x-vertex-shader' id='vertex-shader'>
attribute vec3 vertex, normal;

uniform mat4 projection, view, model, ltransf;
uniform mat3 nmatrix;

varying vec4 position, mposition, luv;
varying vec3 vnormal, vmnormal;

void main() {

  gl_Position = projection * view * model * vec4(vertex, 1.);
  
  position = vec4(model * vec4(vertex, 1.));
  mposition = vec4(vertex, 1.);
  //vnormal = normalize(vec4(model * vec4(normal, 1.)).xyz);
  vmnormal = normalize(normal);
  vnormal = nmatrix * vmnormal;
  luv = (mat4(
      .5, .0, .0, .0,
      .0, .5, .0, .0,
      .0, .0, .5, .0,
      .5, .5, .5, 1.
    ) * ltransf * position);
}

</script>
<script type='application/x-fragment-shader' id='fragment-shader'> 
precision highp float;

#extension GL_OES_standard_derivatives : enable

varying vec4 position, mposition, luv;
varying vec3 vnormal, vmnormal;

uniform vec3 lightPos;
uniform sampler2D texture;

float chebyshevIneq(vec2 moments, float t) {
  if(t <= moments.x)
    return 1.;

  float variance = moments.y - (moments.x * moments.x);
  variance = max(variance, .02);
  float d = t - moments.x;
  variance = variance / (variance + d * d);
  return variance;
}

float unpack(vec2 color) {
  return color.x + (color.y / 255.);
}

////////////////////////////////////////////////////////////////////////////////
// https://github.com/ashima/webgl-noise/blob/master/src/noise2D.glsl         //
vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec2 mod289(vec2 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec3 permute(vec3 x) { return mod289(((x*34.0)+1.0)*x); }
float snoise(vec2 v) {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);
  vec2 i1;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;
  i = mod289(i);
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		+ i.x + vec3(0.0, i1.x, 1.0 ));
  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;
  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}
////////////////////////////////////////////////////////////////////////////////

// Analytical integral antialiasing
#define textureBrickI(x, p, notp) ((floor(x)*(p))+max(fract(x)-(notp), 0.0))
vec3 textureBrick(vec2 uv) {

  const float bW  = .0625,
              bH  = .03125,
              mS  = 1. / 128.,
              mWf = mS * .5 / bW,
              mHf = mS * .5 / bH;
  const vec3  brickColor  = vec3(.5, .0, .1),
              mortarColor = vec3(.5, .5, .5);

  float u = uv.s / bW,
        v = uv.t / bH,
        brU = floor(u),
        brV = floor(v);

  if(mod(v * .5, 1.) > .5)
    u += .5;
  brU = floor(u);

  float noisev = 1. + 
                 snoise(uv * 16.) * .0625 +
                 abs(snoise(uv * 512.)) * .0625;
  float noisei = abs(snoise(64. * vec2(brU, brV)));
  float brickDamp = 1. + .25 * sin(2. * (brU + 1.)) * sin(2. * (brV + 1.));

  vec2 pos = vec2(u, v),
       fw = 2. * fwidth(pos),
       mortarPct = vec2(mWf, mHf),
       brickPct = vec2(1., 1.) - mortarPct,
       ub = (textureBrickI(pos + fw, brickPct, mortarPct) -
             textureBrickI(pos, brickPct, mortarPct)) / fw;

  return mix(mortarColor, brickColor * brickDamp, ub.x * ub.y) * noisev;
}

vec3 textureWindow(vec2 uv) {
  const float wW = 1. / 12.,
              wH = 1. / 12.,
              fS = 1. / 256.,
              fWf = fS * .5 / wW,
              fHf = fS * .5 / wH;
  const vec2 ws = vec2(wW, wH);

  const vec3 windowColor = vec3(.8, .8, .1),
             frameColor = vec3(.5, .5, .5),
             avgColor = (windowColor + frameColor) * .5;

  vec2 uuv = fract(uv + vec2(fS));

  float noisev = 1. + 
                 snoise(uv * 16.) * .0625 +
                 abs(snoise(uv * 512.)) * .0625;
  vec2 e, f;
  vec2 fk = fwidth(uv);
  vec3 mixColor = vec3(0.,0.,0.);
  f = (step(ws + fS, uuv) - 
       step(1. - ws - fS, uuv)) - 10e-3;
  /*f = (smoothstep(ws + fS + fk, ws + fS - fk, uuv) - 
       smoothstep(1. - ws - fS + fk, 1. - ws - fS - fk, uuv));*/

  float fuzz = max(fk.x, fk.y);
  
  e = 1. - (smoothstep(ws - fS / 2. - fk, ws - fS / 2. + fk, uuv) - 
            smoothstep(1. - ws + fS / 2. - fk, 1. - ws + fS / 2. + fk, uuv));
  mixColor = mix(frameColor * noisev, windowColor, e.x * e.y);

  return mix(
    mixColor,
    textureBrick(uv),
    clamp(f.x + f.y, 0., 1.)
  );
}

vec3 textureRoad(vec2 uuv) {
  const float padding = 1. / 32.,
              tapeW   = 1. / 32.,
              vertDiv = 4.;
  const vec3 asphaltColor = vec3(.2, .2, .2),
             stripColor = vec3(.8, .8, .8);

  vec2 uv = uuv + .5, fk = fwidth(uv);
  float q = 
    (
      smoothstep(padding - fk.x, padding + fk.x, uv.s) - 
      smoothstep(padding + tapeW - fk.x, padding + tapeW + fk.x, uv.s)
    ) +
    (
      smoothstep(1. - padding - tapeW - fk.x, 1. - padding - tapeW + fk.x, uv.s) - 
      smoothstep(1. - padding - fk.y, 1. - padding + fk.y, uv.s)
    ) +
    (
      smoothstep(.5 - tapeW * .5 - fk.x, .5 - tapeW * .5 + fk.x, uv.s) - 
      smoothstep(.5 + tapeW * .5 - fk.x, .5 + tapeW * .5 + fk.x, uv.s)
    ) * 
    (
      smoothstep(.5 - fk.y, .5 + fk.y, mod(.25 + uv.t * vertDiv, 1.))
    )
    ;

  float noiseA = 1. +
                 abs(snoise(uv * 16.))  * .0625 +
                 abs(snoise(uv * 32.))  * .0625 +
                 abs(snoise(uv * 128.)) * .125,
        noiseS = 1. + 
                 abs(snoise(uv * 128.)) * .125;

  return mix(asphaltColor * noiseA, stripColor * noiseS, q);
}

vec3 triplanarBlend(vec3 norm) {
  vec3 blend = normalize(max(abs(norm), 10e-5));
  float b = blend.x + blend.y + blend.z;
  blend /= vec3(b,b,b);
  return blend;
}

void main() {

  vec3 ambientColor = vec3(0.5, 0.5, 0.5);
  vec3 depth = luv.xyz;

  vec3 blend = triplanarBlend(vmnormal);
  vec3 tX = textureBrick(mposition.zy * .5),
       tY = textureRoad(mposition.xz),
       tZ = textureWindow(mposition.xy * .5);

  float diffuse = .3 + clamp(max(dot(normalize(lightPos), vnormal), 0.), 0., .7);
  vec4 sample = texture2D(texture, depth.xy);

  /*vec3 color = ambientColor * .5;
  if(diffuse > 0.)
    color += ambientColor * diffuse * .5;*/
  vec3 color = tX * blend.x + tY * blend.y + tZ * blend.z;

  //float illum = chebyshevIneq(vec2(unpack(sample.xy), unpack(sample.zw)), depth.z);
  //illum = 1.;
  float illum = clamp(exp(-4. * (depth.z - unpack(sample.xy))), 0., 1.);

  //gl_FragColor = clamp(vec4(color * illum, 1.), 0., 1.);
  gl_FragColor = clamp(vec4(color * diffuse, 1.), 0., 1.);
}

</script>

<script type='application/x-vertex-shader' id='vertex-shadow'>
attribute vec3 vertex, normal;

uniform mat4 ltransf, model;

void main() {
  
  gl_Position = ltransf * model * vec4(vertex, 1.);
}

</script>
<script type='application/x-fragment-shader' id='fragment-shadow'> 
precision highp float;

vec2 pack(float depth) {
  const vec2 bias = vec2(1. / 255., 0.);
  vec2 col = vec2(depth, fract(depth * 255.));

  return col - (col.yy * bias);
}

void main() {
  float moment = gl_FragCoord.z; 
  gl_FragColor = vec4(pack(moment), pack(moment * moment));
}

</script>
<script src='gl-matrix-min.js'></script>
