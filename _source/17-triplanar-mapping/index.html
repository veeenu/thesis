---
layout: proto
title: "Triplanar procedural texture mapping"
---
<script type='application/x-vertex-shader' id='vertex-shader'>
attribute vec3 vertex, normal;

uniform mat4 projection, view, model, ltransf;
uniform mat3 nmatrix;

varying vec4 position, mposition, luv;
varying vec3 vnormal, vmnormal;

void main() {

  gl_Position = projection * view * model * vec4(vertex, 1.);
  
  position = vec4(model * vec4(vertex, 1.));
  mposition = vec4(vertex, 1.);
  //vnormal = normalize(vec4(model * vec4(normal, 1.)).xyz);
  vmnormal = normalize(normal);
  vnormal = nmatrix * vmnormal;
  luv = (mat4(
      .5, .0, .0, .0,
      .0, .5, .0, .0,
      .0, .0, .5, .0,
      .5, .5, .5, 1.
    ) * ltransf * position);
}

</script>
<script type='application/x-fragment-shader' id='fragment-shader'> 
precision highp float;

#extension GL_OES_standard_derivatives : enable

varying vec4 position, mposition, luv;
varying vec3 vnormal, vmnormal;

uniform vec3 lightPos;

float chebyshevIneq(vec2 moments, float t) {
  if(t <= moments.x)
    return 1.;

  float variance = moments.y - (moments.x * moments.x);
  variance = max(variance, .02);
  float d = t - moments.x;
  variance = variance / (variance + d * d);
  return variance;
}

float unpack(vec2 color) {
  return color.x + (color.y / 255.);
}

////////////////////////////////////////////////////////////////////////////////
// https://github.com/ashima/webgl-noise/blob/master/src/noise2D.glsl         //
vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec2 mod289(vec2 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec3 permute(vec3 x) { return mod289(((x*34.0)+1.0)*x); }
float snoise(vec2 v) {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);
  vec2 i1;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;
  i = mod289(i);
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		+ i.x + vec3(0.0, i1.x, 1.0 ));
  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;
  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}
////////////////////////////////////////////////////////////////////////////////

// Analytical integral antialiasing
#define textureBrickI(x, p, notp) ((floor(x)*(p))+max(fract(x)-(notp), 0.0))
vec3 textureBrick(vec2 uv) {

  const float bW  = .0625,
              bH  = .03125,
              mS  = 1. / 128.,
              mWf = mS * .5 / bW,
              mHf = mS * .5 / bH;
  const vec3  brickColor  = vec3(.5, .0, .1),
              mortarColor = vec3(.5, .5, .5);

  float u = uv.s / bW,
        v = uv.t / bH,
        brU = floor(u),
        brV = floor(v);

  if(mod(v * .5, 1.) > .5)
    u += .5;
  brU = floor(u);

  float noisev = 1. + 
                 snoise(uv * 16.) * .0625 +
                 abs(snoise(uv * 512.)) * .0625;
  float noisei = abs(snoise(64. * vec2(brU, brV)));
  float brickDamp = 1. + .25 * sin(2. * (brU + 1.)) * sin(2. * (brV + 1.));

  vec2 pos = vec2(u, v),
       fw = 2. * vec2(fwidth(pos.x), fwidth(pos.y)),
       mortarPct = vec2(mWf, mHf),
       brickPct = vec2(1., 1.) - mortarPct,
       ub = (textureBrickI(pos + fw, brickPct, mortarPct) -
             textureBrickI(pos, brickPct, mortarPct)) / fw;

  return mix(mortarColor, brickColor * brickDamp, ub.x * ub.y) * noisev;
}

vec3 textureWindow(vec2 uuv) {

  const vec2 patternPct   = vec2(1. / 2., 1. / 2.),
             patternStart = (1. - patternPct) * .5,
             patternEnd   = patternStart + patternPct,
             framePct     = vec2(1. / 64., 1. / 64.),
             frameStart   = patternStart + framePct,
             frameEnd     = patternEnd   - framePct;
  const vec3 windowColor  = vec3(.8, .8, .1),
             frameColor   = vec3(.5, .5, .5);

  vec2 uv   = mod(2. * uuv + .5, 1.),
       fk   = fwidth(uv),
       patQ = (smoothstep(patternStart - fk, patternStart + fk, uv) -
              smoothstep(patternEnd - fk, patternEnd + fk, uv)) *
              (smoothstep(vec2(0.), fk, uv) * (1. - smoothstep(1. - fk, vec2(1.), uv))), // Remove edges
       patF = (smoothstep(frameEnd - fk, frameEnd + fk, uv) - smoothstep(frameStart - fk, frameStart + fk, uv));
  float noisep = 1. + 
                 snoise(-uv * 2.) * .125;
  float noisev = 1. + 
                 snoise(uuv * 16.) * .0625 +
                 abs(snoise(uuv * 512.)) * .0625;

  return mix(textureBrick(uuv), mix(frameColor * noisev, windowColor * noisep, patF.x * patF.y), patQ.x * patQ.y);

}

vec3 textureRoad(vec2 uuv) {
  const float padding = 1. / 32.,
              tapeW   = 1. / 32.,
              vertDiv = 4.;
  const vec3 asphaltColor = vec3(.2, .2, .2),
             stripColor = vec3(.8, .8, .8);

  vec2 uv = uuv + .5, fk = fwidth(uv);
  float q = 
    (
      smoothstep(padding - fk.x, padding + fk.x, uv.s) - 
      smoothstep(padding + tapeW - fk.x, padding + tapeW + fk.x, uv.s)
    ) +
    (
      smoothstep(1. - padding - tapeW - fk.x, 1. - padding - tapeW + fk.x, uv.s) - 
      smoothstep(1. - padding - fk.y, 1. - padding + fk.y, uv.s)
    ) +
    (
      smoothstep(.5 - tapeW * .5 - fk.x, .5 - tapeW * .5 + fk.x, uv.s) - 
      smoothstep(.5 + tapeW * .5 - fk.x, .5 + tapeW * .5 + fk.x, uv.s)
    ) * 
    (
      smoothstep(.5 - fk.y, .5 + fk.y, mod(.25 + uv.t * vertDiv, 1.)) *
      (1. - smoothstep(1. - 2. * fk.y, 1., mod(.25 + uv.t * vertDiv, 1.)))
    )
    ;

  float noiseA = 1. +
                 abs(snoise(uv * 16.))  * .0625 +
                 abs(snoise(uv * 32.))  * .0625 +
                 abs(snoise(uv * 128.)) * .125,
        noiseS = 1. + 
                 abs(snoise(uv * 128.)) * .125;

  return mix(asphaltColor * noiseA, stripColor * noiseS, q);
}

vec3 triplanarBlend(vec3 norm) {
  vec3 blend = normalize(max(abs(norm), 10e-5));
  float b = blend.x + blend.y + blend.z;
  blend /= vec3(b,b,b);
  return blend;
}

void main() {

  vec3 ambientColor = vec3(0.5, 0.5, 0.5);
  vec3 depth = luv.xyz;

  vec3 blend = triplanarBlend(vmnormal);
  vec3 tX = textureBrick(mposition.zy * .5),
       tY = textureRoad(mposition.xz),
       tZ = textureWindow(mposition.xy * .5);

  float diffuse = .3 + clamp(max(dot(normalize(lightPos), vnormal), 0.), 0., .7);
  vec3 color = tX * blend.x + tY * blend.y + tZ * blend.z;

  gl_FragColor = clamp(vec4(color * diffuse, 1.), 0., 1.);
}

</script>

<script src='gl-matrix-min.js'></script>
