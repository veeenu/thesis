---
layout: proto
title: "Shape grammar objects - Furniture"
---
<script type='application/x-vertex-shader' id='vertex-shader'>
attribute vec3 vertex, normal, extra;

uniform mat4 projection, view, model, ltransf;
uniform mat3 nmatrix;

varying vec4 position, luv;
varying vec3 vnormal, vuv;

void main() {

  gl_Position = projection * view * model * vec4(vertex, 1.);
  
  position = vec4(model * vec4(vertex, 1.));
  //vnormal = normalize(vec4(model * vec4(normal, 1.)).xyz);
  vnormal = nmatrix * normalize(normal);
  vuv = extra - .5;
  luv = (mat4(
      .5, .0, .0, .0,
      .0, .5, .0, .0,
      .0, .0, .5, .0,
      .5, .5, .5, 1.
    ) * ltransf * position);
}

</script>
<script type='application/x-fragment-shader' id='fragment-shader'> 
#extension GL_OES_standard_derivatives : enable
precision highp float;

varying vec4 position, luv;
varying vec3 vnormal, vuv;

uniform vec3 lightPos;
uniform sampler2D texture;

////////////////////////////////////////////////////////////////////////////////
// https://github.com/ashima/webgl-noise/blob/master/src/noise2D.glsl         //
////////////////////////////////////////////////////////////////////////////////

vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec2 mod289(vec2 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec3 permute(vec3 x) { return mod289(((x*34.0)+1.0)*x); }
float snoise(vec2 v) {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);
  vec2 i1;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;
  i = mod289(i);
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		+ i.x + vec3(0.0, i1.x, 1.0 ));
  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;
  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

////////////////////////////////////////////////////////////////////////////////

float chebyshevIneq(vec2 moments, float t) {
  if(t <= moments.x)
    return 1.;

  float variance = moments.y - (moments.x * moments.x);
  variance = max(variance, .02);
  float d = t - moments.x;
  variance = variance / (variance + d * d);
  return variance;
}

float unpack(vec2 color) {
  return color.x + (color.y / 255.);
}

vec3 wood(vec3 str) {
  const vec3 color0 = vec3(.64, .48, .11),
             color1 = vec3(.49, .34, .08);

  float noise = 
    .5 * snoise(str.xz * .125) * snoise(str.xy * .125) +
    .25 * (snoise(str.xz * .25) * snoise(str.xy * .25)) +
    .125 * (snoise(str.xz * .5) * snoise(str.xy * .5)) +
    .0625 * (snoise(str.xz) * snoise(str.xy));

  return mix(color0, color1, mod(length(str.xz) + noise, 1.));
}

void main() {

  vec3 ambientColor = vec3(1.);
  vec3 depth = luv.xyz;
  vec3 l = normalize(lightPos), n = normalize(vnormal);

  float diffuse = 
    .3 +
    max(dot(l, n), 0.) +
    max(dot(-l, n) * .25, 0.);
  vec4 sample = texture2D(texture, depth.xy);

  vec3 color = wood(8. * vuv) * diffuse * .5;

  float illum = clamp(exp(-4. * (depth.z - unpack(sample.xy))), 0., 1.);
  illum = 1.;

  gl_FragColor = clamp(vec4(color * illum, 1.), 0., 1.);
}

</script>

<script src='gl-matrix-min.js'></script>
<script src='ShapeGrammar.js'></script>
<script src='SHAPE.js'></script>
<script src='Geom.js'></script>
<script src='MersenneTwister.js'></script>
<script src='meshes.js'></script>
